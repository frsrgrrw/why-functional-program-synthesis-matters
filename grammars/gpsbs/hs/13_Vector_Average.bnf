<code> ::= <double>
<double_list_var> ::= 'xs'
<arith_ops> ::= '+'|'-'|'*'
<int_arith_ops_protected> ::= 'safeIntDiv'|'safeMod'
<double_arith_ops_protected> ::= 'safeDiv'|'safeDoubleMod'
<prefix> ::= ''|'-'
<comp_op> ::= '=='|'/='|'<'|'>'|'>='|'<='
<fold> ::= 'foldr'|'foldl'
<int> ::= '('<prefix>' '<int>')'
                        |'(abs '<int>')'
                        |'(('<arith_ops>') '<int>' '<int>')'
                        |'('<int_arith_ops_protected>' '<int>' '<int>')'
                        |'(min '<int>' '<int>')'
                        |'(max '<int>' '<int>')'
                        |'(ceiling ('<double>'))'
                        |'(floor ('<double>'))'
                        |'(round ('<double>'))'
                        |'(truncate ('<double>'))'
                        |'(length '<double_list>')'
<double> ::= '('<prefix>' '<double>')'
                        |'(abs '<double>')'
                        |'(sin '<double>')'
                        |'(cos '<double>')'
                        |'(tan '<double>')'
                        |'(('<arith_ops>') '<double>' '<double>')'
                        |'('<double_arith_ops_protected>' '<double>' '<double>')'
                        |'(min '<double>' '<double>')'
                        |'(max '<double>' '<double>')'
                        |'(fromIntegral ('<int>'))'
                        |'(safeMinimumDoubles '<double_list>')'
                        |'(safeMaximumDoubles '<double_list>')'
                        |'(safeHeadDoubles '<double_list>')'
                        |'(safeLastDoubles '<double_list>')'
                        |'(sum '<double_list>')'
                        |'('<fold>' ('<arith_ops>') '<double>' '<double_list>')'
                        |'('<fold>' '<double_arith_ops_protected>' '<double>' '<double_list>')'
<double_list> ::= <double_list_value>|<double_list_expression>
<double_list_value> ::= <double_list_var>
<double_list_expression> ::= '(filter ('<comp_op>' '<double>') '<double_list>')'
                        |'(safeReplicate '<int>' '<double>')'
                        |'(zipWith ('<arith_ops>') '<double_list>' '<double_list>')'
                        |'(zipWith '<double_arith_ops_protected>' '<double_list>' '<double_list>')'
                        |'(map sin '<double_list>')'
                        |'(map cos '<double_list>')'
                        |'(map tan '<double_list>')'
                        |'('<double>':'<double_list>')'
                        |'('<double_list>' ++ '<double_list>')'
                        |'(safeTail '<double_list>')'
                        |'(safeInit '<double_list>')'
                        |'(take '<int>' '<double_list>')'
                        |'(drop '<int>' '<double_list>')'
